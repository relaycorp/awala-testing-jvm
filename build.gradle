import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript{
  ext {
    kotlinVersion = '1.5.20'
    kotlinCoroutinesVersion = '1.5.1'
    junit5Version = '5.7.2'
  }
}

plugins {
  id("org.jetbrains.kotlin.jvm") version "$kotlinVersion"
  id('java-library')
  id("org.jetbrains.dokka") version "1.4.32"
  id("com.diffplug.spotless") version "5.14.0"
  id('idea')

  id('signing')
  id('maven-publish')
  id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

group = "tech.relaycorp"

repositories {
  mavenCentral()
}

dependencies {
  // Use a version range to make it easy to upgrade the Relaynet library in the application code
  api 'tech.relaycorp:awala:[1.48.5,2.0.0)'

  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"

  testImplementation("org.jetbrains.kotlin:kotlin-test")
  testImplementation("org.junit.jupiter:junit-jupiter:$junit5Version")
  testImplementation("org.junit.jupiter:junit-jupiter-params:$junit5Version")
  testImplementation("org.jetbrains.kotlin:kotlin-test-junit5")
  testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutinesVersion")
}

kotlin {
  explicitApi()
}

java {
  withJavadocJar()
  withSourcesJar()
}

tasks.withType(KotlinCompile).configureEach {
  kotlinOptions.jvmTarget = "1.8"
}

tasks.withType(KotlinCompile).all {
  kotlinOptions.freeCompilerArgs = kotlinOptions.freeCompilerArgs + [
    "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
    "-Xuse-experimental=kotlinx.coroutines.FlowPreview",
    "-Xuse-experimental=kotlin.time.ExperimentalTime"
  ]
}

test {
  useJUnitPlatform()
}

// Documentation

dokkaHtml.configure {
  dokkaSourceSets {
    configureEach {
      reportUndocumented.set(true)
    }
  }
}

// Publishing

signing {
  useGpgCmd()
  required { gradle.taskGraph.hasTask("publish") }
  def signingKeyId = findProperty("signingKeyId")
  def signingKey = findProperty("signingKey")
  def signingPassword = findProperty("signingPassword")
  useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
  sign publishing.publications
}
publishing {
  publications {
    create("default", MavenPublication) {
      from(components["java"])

      pom {
        name.set(rootProject.name)
        description.set("Testing utilities for Awala implementations on the JVM")
        url.set("https://github.com/relaycorp/awala-testing-jvm")
        developers {
          developer {
            id.set("relaycorp")
            name.set("Relaycorp, Inc.")
            email.set("no-reply@relaycorp.tech")
          }
        }
        licenses {
          license {
            name.set("Apache-2.0")
          }
        }
        scm {
          connection.set("scm:git:https://github.com/relaycorp/awala-testing-jvm.git")
          developerConnection.set(
            "scm:git:https://github.com/relaycorp/awala-testing-jvm.git"
          )
          url.set("https://github.com/relaycorp/awala-testing-jvm")
        }
      }
    }
  }
  repositories {
    maven {
      url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
      credentials {
        username = System.getenv("MAVEN_USERNAME")
        password = System.getenv("MAVEN_PASSWORD")
      }
    }
  }
}
nexusPublishing {
  repositories {
    sonatype {
      nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
      snapshotRepositoryUrl.set(
              uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
      )
      username.set(System.getenv("MAVEN_USERNAME"))
      password.set(System.getenv("MAVEN_PASSWORD"))
    }
  }
}
tasks.publish {
  finalizedBy("closeAndReleaseSonatypeStagingRepository")
}

// Lint

spotless {
  def ktlintUserData = [
    "max_line_length": "100",
    "disabled_rules": "import-ordering"
  ]
  kotlin {
    ktlint("0.37.0").userData(ktlintUserData)
  }
  kotlinGradle {
    ktlint().userData(ktlintUserData)
  }
}
